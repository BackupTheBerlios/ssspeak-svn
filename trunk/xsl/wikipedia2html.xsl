<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                version="1.0">

  <xsl:output method="html" media-type="text/html" version="4.0" indent="yes" encoding="utf-8" omit-xml-declaration="yes"/>

  <xsl:template match="/">
    <xsl:apply-templates select="html"/>
  </xsl:template>

  <xsl:template match="html">
    <xsl:comment> Generated by wikipedia2html.xml </xsl:comment>
    <xsl:element name="{local-name(.)}">
      <xsl:apply-templates select="head|body|@*"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="body">
    <xsl:element name="{local-name(.)}">
      <xsl:apply-templates select="descendant::div[@id = 'content']/*|@*"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="head">
    <xsl:element name="{local-name(.)}">
      <xsl:apply-templates select="*|@*"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="div[@class = 'editsection'] | span[@class = 'urlexpansion']">
  </xsl:template>

  <xsl:template match="table[@id = 'toc']">
  </xsl:template>

  <xsl:template match="@*|text()|comment()">
    <xsl:copy-of select="."/>
  </xsl:template>

  <xsl:template match="*[@id='siteSub']"/>

  <xsl:template match="*">
    <xsl:element name="{local-name(.)}">
      <xsl:apply-templates select="*|@*|text()|comment()"/>
    </xsl:element>
  </xsl:template>
</xsl:stylesheet>
